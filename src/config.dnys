# AquaShell Photo Discord Bot config manager script file

global INI_BotToken string;
global INI_PhotoChannel string;
global INI_ChatMessage string;

function CFG_ReadSettings bool(inifile string)
{
    local checkfile bool;

    result false;

    fexists "%inifile" checkfile;
    if (%checkfile, -eq, true) {
        ini_read "%inifile" "settings" "token" INI_BotToken;
        ini_read "%inifile" "settings" "channel" INI_PhotoChannel;
        ini_read "%inifile" "settings" "message" INI_ChatMessage;

        result true;
    };
};

function CFG_YearDay string(ydfile string)
{
    local filehandle int;
    local isopened bool;
    local cntfile string;

    result "";

    set cntfile <= "0";

    fopen "%ydfile" false filehandle;
    fisopen %filehandle isopened;
    if (%isopened, -eq, true) {
        freadline %filehandle cntfile;

        fclose %filehandle;
    };

    if ("%cntfile", -eq, "") {
        set cntfile <= "0";
    };

    result "%cntfile";
};

function CFG_UpdateYD void(ydfile string, newcontent string)
{
    local filehandle int;
    local doesexist bool;
    local isopened bool;

    fexists "%ydfile" doesexist;
    if (%doesexist, -eq, true) {
        fremove "%ydfile" void;
    };

    fopen "%ydfile" false filehandle;
    fisopen %filehandle isopened;
    if (%isopened, -eq, true) {
        fwriteline %filehandle "%newcontent";

        fclose %filehandle;
    };
};
