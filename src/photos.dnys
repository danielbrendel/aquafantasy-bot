# AquaShell Photo Discord Bot photo manager script file

array CurrentEnumeratedPhotos string 0 ();
array PreviousPhotoList string 0 ();

function EnumImageFolder int(baseObject string, iteratedObject string)
{
	array_item_append CurrentEnumeratedPhotos "%baseObject%iteratedObject";

    result 1;
};

function AcquirePhotoList void(photopath string)
{
	local enumres bool;

	denum {%photopath/} "*.*" "EnumImageFolder" enumres;
};

function AcquireHistoryList void(histlist string)
{
	local filehandle int;
	local fileopened bool;
	local eofreached bool;
	local curreadline string;

	set eofreached <= false;

	fopen "%histlist" false filehandle;
	fisopen %filehandle fileopened;
	if (%fileopened, -eq, true) {
		while (%eofreached, -eq, false) {
			freadline %filehandle curreadline;
			fateof %filehandle eofreached;

			array_item_append PreviousPhotoList "%curreadline";
		};

		fclose %filehandle;
	};
};

function AddToHistoryList void(histlist string, addfile string)
{
	local filehandle int;
	local fileopened bool;

	fopen "%histlist" true filehandle;
	fisopen %filehandle fileopened;
	if (%fileopened, -eq, true) {
		fwriteline %filehandle "%addfile";

		fclose %filehandle;
	};
};

function ResetHistoryList void(histlist string)
{
	local opresult bool;

	fremove "%histlist" opresult;
};

function IsAlreadyInHistoryList bool(selphoto string)
{
	result false;

	for (cnt, 0, %PreviousPhotoList.length, -inc) {
		if ("%PreviousPhotoList[%cnt]", -eq, "%selphoto") {
			result true;
		};
	};
};
